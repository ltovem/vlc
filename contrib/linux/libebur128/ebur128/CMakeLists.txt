set(BUILD_STATIC_LIBS       ON  CACHE BOOL "Build static library")
set(WITH_STATIC_PIC         OFF CACHE BOOL "Compile static library with -fPIC flag")
set(ENABLE_INTERNAL_QUEUE_H OFF CACHE BOOL "Use own queue.h")

#### queue.h
file(WRITE ${CMAKE_CURRENT_BINARY_DIR}/queuetest.c
"#include <sys/queue.h>\nLIST_HEAD(listhead, entry) head;\nint main() { return 0; }")
try_compile(HAS_QUEUE ${CMAKE_CURRENT_BINARY_DIR}
                      ${CMAKE_CURRENT_BINARY_DIR}/queuetest.c)

set(SUMMARY_HAS_QUEUE ${HAS_QUEUE} CACHE INTERNAL "")

if(ENABLE_INTERNAL_QUEUE_H)
  include_directories(SYSTEM queue)
endif()


if(MSVC)
  add_definitions(-D_USE_MATH_DEFINES)
  add_definitions(/arch:SSE2)
  add_definitions(-D __SSE2_MATH__)
endif()


set(EBUR128_VERSION_MAJOR 1)
set(EBUR128_VERSION 1.2.4)

add_library(ebur128 ebur128.c)

if(NOT BUILD_SHARED_LIBS)
  # Static build specific things
  if(WITH_STATIC_PIC)
    set_property(TARGET ebur128 PROPERTY POSITION_INDEPENDENT_CODE ON)

    set_target_properties(ebur128 PROPERTIES
      SOVERSION ${EBUR128_VERSION_MAJOR}
      VERSION ${EBUR128_VERSION})
  endif()

else()
  # Share build specific things
  set_target_properties(ebur128 PROPERTIES
    SOVERSION ${EBUR128_VERSION_MAJOR}
    VERSION ${EBUR128_VERSION})

  if(WIN32)
    set_target_properties(ebur128 PROPERTIES
      OUTPUT_NAME ebur128
      RUNTIME_OUTPUT_NAME ebur128-${EBUR128_VERSION_MAJOR}
      ARCHIVE_OUTPUT_NAME ebur128)
  endif(WIN32)

  if(MSVC)
    target_sources(ebur128 PRIVATE ebur128.def)
  endif()
endif()

# Link with Math library if available
find_library(MATH_LIBRARY m)
if(MATH_LIBRARY)
  target_link_libraries(ebur128 ${MATH_LIBRARY})
endif()

set(EBUR128_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR} CACHE INTERNAL "")

install(FILES ebur128.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
install(TARGETS ebur128
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR})

#### pkg-config
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/libebur128.pc.cmake
               ${CMAKE_CURRENT_BINARY_DIR}/libebur128.pc @ONLY)
install(FILES "${CMAKE_CURRENT_BINARY_DIR}/libebur128.pc"
        DESTINATION ${CMAKE_INSTALL_LIBDIR}/pkgconfig)
